#include "/Engine/Public/Platform.ush"
#include "GrassData.ush"

float GlobalWorldTime;
#include "Noise.usf"

float Lambda;
float MinHeight;
float MaxHeight;

int Size;
StructuredBuffer<GrassData> GrassDataBuffer;

RWStructuredBuffer<float3> GrassPoints;
RWStructuredBuffer<int> GrassTriangles;


static float G = 9.81;
static int maxSteps = 7;
static int minSteps = 7;

void addPoint(in uint pointIdx, in float3 p)
{
    GrassPoints[pointIdx] = p;
}

void addTri(in uint triIdx, in uint idx1, in uint idx2, in uint idx3)
{
    GrassTriangles[triIdx + 0] = idx1;
    GrassTriangles[triIdx + 1] = idx2;
    GrassTriangles[triIdx + 2] = idx3;
}

float3 quadBez(in float p0, in float p1, in float p2, in float t)
{
    return lerp(p0, lerp(p1, p2, t), t);
}

float invTan(in float x)
{
    return 2 * atan(x) / 3.141;
}

//  z
//  |
//  |
//  |         * *         -
//  |    y  *     *       |
//  |   /  *              | height
//  |  /  *               |
//  | /  *                |
//  |/   *                -
//  +-------------------------- x
//  0
float3 computeInitialVelocity(in float3 seed, in float height)
{
    float3 v0 = seed;
    v0.xy += sign(v0.xy) * 1.5;
    v0.z = sqrt(2 * G * height);
    return v0;
}

float3 movePoint(in float3 pi, in float3 vi, in float t)
{
    float3 p = pi + vi * t;
    p.z -= t * t * G / 2;
    return p;
}

//  z
//  |
//  |
//  |         * *
//  |    y  *     * pe (t = te)
//  |   /  *    
//  |  /  *
//  | /  *
//  |/   * p0 (t = 0)
//  +-------------------------- x
//  0
void createGrass(in uint idx, in float3 p0, in int steps)
{
    // initial condictions
    float3 ext = rand3(p0);
    
    float height = lerp(MinHeight, MaxHeight, (ext.x + ext.y + ext.z) / 3);
    
    float3 v0 = computeInitialVelocity(ext, height);
    float epsilon = (snoise(p0 / 1000) - 0.5f) * Lambda;
    float te = (v0.z - epsilon) / G;
    
    float maxThickness = height * 0.02;
    float3 normal = normalize(cross(v0, float3(v0.xy, 0)));
    
    uint pointIdx = idx * (maxSteps * 2 + 1);
    uint triangleIdx = idx * (maxSteps * 2 - 1) * 2; // '* 2' per i triangoli dietro
    
    uint grassPIdx = pointIdx;
    uint grassTIdx = triangleIdx * 3;
    for (uint i = 0; i < steps; i++, grassTIdx += 12, grassPIdx += 2)
    {
        float perc = float(i) / steps;
        float t = perc * te;
        
        float3 p = movePoint(p0, v0, t);
        float thickness = quadBez(maxThickness, maxThickness * 0.85, 0, perc);
        
        // p1
        float3 p1 = p - normal * thickness;
        addPoint(grassPIdx + 0, p1);
        
        // p2
        float3 p2 = p + normal * thickness;
        addPoint(grassPIdx + 1, p2);
        
        
        if (i < steps - 1)
        {
            // t1
            addTri(grassTIdx + 0, grassPIdx + 0, grassPIdx + 1, grassPIdx + 3); // front
            addTri(grassTIdx + 3, grassPIdx + 0, grassPIdx + 3, grassPIdx + 1); // back
                
            // t2
            addTri(grassTIdx + 6, grassPIdx + 0, grassPIdx + 3, grassPIdx + 2); // front
            addTri(grassTIdx + 9, grassPIdx + 0, grassPIdx + 2, grassPIdx + 3); // back
        }
        else
        {
            // last triangle
            addTri(grassTIdx + 0, grassPIdx + 0, grassPIdx + 1, grassPIdx + 2); // front
            addTri(grassTIdx + 3, grassPIdx + 0, grassPIdx + 2, grassPIdx + 1); // back
        }
        
    }
    
    // last point
    float3 pe = movePoint(p0, v0, te);
    addPoint(grassPIdx, pe);
}


[numthreads(THREADS_X, THREADS_Y, THREADS_Z)]
void Main(
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID,
	uint3 GroupId : SV_GroupID,
	uint GroupIndex : SV_GroupIndex)
{
    if (DispatchThreadId.x >= Size)
        return;
    
    uint id = DispatchThreadId.x;
    
    float3 p0 = GrassDataBuffer[id].position;
    
    createGrass(id, p0, 7);
}